apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = "0.1.0"
group = "io.github.cadiboo.optifinedeobf"
archivesBaseName = "OptiFineDeobf"

repositories {
	mavenCentral()
}

dependencies {
	compile "org.ow2.asm:asm:7.2"
	compile "org.ow2.asm:asm-tree:7.2"
	compile "org.ow2.asm:asm-util:7.2"
}

def commonManifest = {
	attributes([
			"Main-Class"              : "io.github.cadiboo.optifinedeobf.DeobfFrame",
			"Implementation-Title"    : "OptiFineDeobf",
			"Implementation-Version"  : "${version}",
			"Implementation-Vendor"   : group,
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"Signing-Fingerprint"     : project.hasProperty('signSHA1') ? project.findProperty('signSHA1') : "unsigned",
			'Built-With'              : 'Gradle v' + project.getGradle().getGradleVersion()
	],)
}

jar {
	manifest commonManifest
	classifier = 'bin'
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	manifest commonManifest
	from sourceSets.main.allSource
}
build.dependsOn sourcesJar

task fatJar(type: Jar, dependsOn: 'jar') {
	inputs.file jar.archivePath
	from(configurations.compile.collect { zipTree(it).matching { exclude 'META-INF/**' } })
	manifest commonManifest
	classifier = 'all'
}

fatJar.doFirst {
	from zipTree(jar.archivePath)
}

artifacts {
	archives sourcesJar
	archives fatJar
}
